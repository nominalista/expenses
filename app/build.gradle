apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

android {

    compileSdkVersion 28

    defaultConfig {
        applicationId "com.nominalista.expenses"
        versionCode 15
        versionName "0.4.1"
        minSdkVersion 21
        targetSdkVersion 28

        // This indicates to Room where it should export schema of database.
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        // Enables AndroidJUnit4 runner in tests.
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        // Helps Room to find schema files for testing migrations.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            versionNameSuffix "-dev"
            applicationIdSuffix ".dev"
            // Use only English resources to build faster.
            resConfigs "en", "xxhdpi"
        }

        prod {}
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {

    def dependencies = rootProject.ext.appDependencies
    def testDependencies = rootProject.ext.appTestDependencies

    configurations.all {
        resolutionStrategy {
            force 'com.google.code.gson:gson:2.8.0'
        }
    }

    // Kotlin
    implementation dependencies.kotlin

    // AndroidX
    implementation dependencies.appCompat
    implementation dependencies.constraintLayout
    implementation dependencies.coreKtx
    implementation dependencies.lifecycleExtensions
    implementation dependencies.lifecycleReactiveStreams
    implementation dependencies.preference
    implementation dependencies.recyclerView
    implementation dependencies.roomRuntime
    implementation dependencies.roomRxJava
    implementation dependencies.workRuntime
    implementation dependencies.workRxJava
    kapt dependencies.roomCompiler

    // Firebase
    implementation dependencies.firebaseAuth
    implementation dependencies.firebaseConfig
    implementation dependencies.firebaseCore
    implementation dependencies.firebaseFirestore
    implementation dependencies.firebaseMessaging

    // Google Play
    implementation dependencies.googlePlayBilling
    implementation dependencies.googlePlayServicesAuth

    // Libraries
    implementation dependencies.coroutinesAndroid
    implementation dependencies.coroutinesCore
    implementation dependencies.jxl
    implementation dependencies.material
    implementation dependencies.navigationFragment
    implementation dependencies.navigationUi
    implementation dependencies.rxAndroid
    implementation dependencies.rxKotlin
    implementation dependencies.rxRelay
    implementation dependencies.threeTen

    // Testing
    testImplementation testDependencies.jUnit
    androidTestImplementation testDependencies.jUnit
    androidTestImplementation testDependencies.core
    androidTestImplementation testDependencies.rules
    androidTestImplementation testDependencies.runner
    androidTestImplementation testDependencies.roomTesting
}

apply plugin: 'com.google.gms.google-services'
